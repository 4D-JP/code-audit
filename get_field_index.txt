$dataTypeMap:=New collection
$dataTypeMap[1]:="boolean"
  //
$dataTypeMap[3]:="16-bit integer"
$dataTypeMap[4]:="32-bit integer"
$dataTypeMap[5]:="64-bit integer"
$dataTypeMap[6]:="real"
$dataTypeMap[7]:="float"
$dataTypeMap[8]:="date"
$dataTypeMap[9]:="time"
$dataTypeMap[10]:="text inside record"
  //11
$dataTypeMap[12]:="picture"
$dataTypeMap[13]:="UUID"
$dataTypeMap[14]:="text outside record"
  //15
  //16
  //17
$dataTypeMap[18]:="BLOB"
$dataTypeMap[19]:="text inside record"  //utf8
$dataTypeMap[20]:="text outside record"  //utf8
$dataTypeMap[21]:="object"

ARRAY LONGINT($fieldTypes;0)

Begin SQL
	SELECT DISTINCT(DATA_TYPE) 
	FROM _USER_COLUMNS,_USER_IND_COLUMNS
	WHERE _USER_IND_COLUMNS.TABLE_ID = _USER_COLUMNS.TABLE_ID
	AND _USER_IND_COLUMNS.COLUMN_ID = _USER_COLUMNS.COLUMN_ID
	INTO :$fieldTypes
End SQL

$results:=New collection

$indexTypes:=New collection
$indexTypes[0]:="B-Tree"
$indexTypes[1]:="Cluster B-Tree"
$indexTypes[2]:="Composite"
$indexTypes[3]:="Keyword"
$indexTypes[4]:="Auto"
$indexTypes[5]:="Object"

For ($i;1;Size of array($fieldTypes))
	
	ARRAY LONGINT($types;0)
	ARRAY TEXT($id;0)
	
	$type:=$fieldTypes{$i}
	
	Begin SQL
		SELECT _USER_INDEXES.INDEX_TYPE,_USER_INDEXES.INDEX_ID 
		FROM _USER_COLUMNS,_USER_IND_COLUMNS,_USER_INDEXES
		WHERE DATA_TYPE = :$type
		AND _USER_IND_COLUMNS.TABLE_ID = _USER_COLUMNS.TABLE_ID
		AND _USER_IND_COLUMNS.COLUMN_ID = _USER_COLUMNS.COLUMN_ID
		AND _USER_INDEXES.INDEX_ID = _USER_IND_COLUMNS.INDEX_ID
		INTO :$types,:$id;
	End SQL
	
	$composite:=0
	$keyword:=0
	For ($j;1;Size of array($id))
		$INDEX_ID:=$id{$j}
		$count:=0
		$count2:=0
		Begin SQL
			SELECT COUNT(*)
			FROM _USER_IND_COLUMNS
			WHERE INDEX_ID = :$INDEX_ID
			INTO :$count;
			
			SELECT COUNT(*)
			FROM _USER_INDEXES
			WHERE INDEX_ID = :$INDEX_ID
			AND KEYWORD = TRUE
			INTO :$count2;
		End SQL
		If ($count>1)
			$composite:=$composite+1
		End if 
		If ($count2#0)
			$keyword:=$keyword+1
		End if 
		
	End for 
	
	$results.push(New collection($dataTypeMap[$type];\
	Count in array($types;1)-$composite;\
	Count in array($types;3)-$keyword;\
	Count in array($types;7)+Count in array($types;8);\
	$composite;$keyword).join("\t"))
	
End for 

SET TEXT TO PASTEBOARD($results.join("\r"))
