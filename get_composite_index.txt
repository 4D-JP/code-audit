ARRAY TEXT($id;0)

Begin SQL
	SELECT DISTINCT(INDEX_ID)
	FROM _USER_IND_COLUMNS
	INTO :$id;
End SQL

$count:=0
$results:=New collection

For ($i;1;Size of array($id))
	$INDEX_ID:=$id{$i}
	Begin SQL
		SELECT COUNT(*)
		FROM _USER_IND_COLUMNS
		WHERE INDEX_ID = :$INDEX_ID
		INTO :$count;
	End SQL
	
	If ($count>1)
		ARRAY TEXT($fields;0)
		Begin SQL
			SELECT DISTINCT(CONCAT(CONCAT(CONCAT('[:',TABLE_ID),']:'),COLUMN_ID))
			FROM _USER_IND_COLUMNS
			WHERE INDEX_ID = :$INDEX_ID
			INTO :$fields;
		End SQL
		
		$f:=New collection
		
		For ($j;1;Size of array($fields))
			$f.push(Parse formula($fields{$j};Formula out with tokens))
		End for 
		
		$results.push(New collection($INDEX_ID;$f.join(",")).join("\t"))
		
	End if 
	
End for 

SET TEXT TO PASTEBOARD($results.join("\r"))
