$indexTypeMap:=New collection
$indexTypeMap[1]:="b-tree"
$indexTypeMap[3]:="cluster"
$indexTypeMap[7]:="auto"
$indexTypeMap[8]:="object"

ARRAY LONGINT($types;0)

  //N-field
Begin SQL
	SELECT DISTINCT(_USER_INDEXES.INDEX_TYPE)
	FROM _USER_CONS_COLUMNS,_USER_COLUMNS,_USER_CONSTRAINTS,_USER_INDEXES,_USER_IND_COLUMNS
	WHERE _USER_CONSTRAINTS.CONSTRAINT_TYPE= '4DR'
	AND _USER_CONSTRAINTS.CONSTRAINT_ID = _USER_CONS_COLUMNS.CONSTRAINT_ID
	AND _USER_COLUMNS.TABLE_ID = _USER_CONSTRAINTS.TABLE_ID
	AND _USER_COLUMNS.COLUMN_ID = _USER_CONS_COLUMNS.COLUMN_ID
	AND _USER_COLUMNS.TABLE_ID = _USER_IND_COLUMNS.TABLE_ID
	AND _USER_COLUMNS.COLUMN_ID = _USER_IND_COLUMNS.COLUMN_ID
	AND _USER_INDEXES.INDEX_ID = _USER_IND_COLUMNS.INDEX_ID
	INTO :$types;
End SQL

$types1:=New collection
ARRAY TO COLLECTION($types1;$types)

C_LONGINT($type)

$results:=New collection

ARRAY TEXT($fields;0)

For each ($type;$types1)
	
	Begin SQL
		SELECT DISTINCT(CONCAT(CONCAT(_USER_COLUMNS.TABLE_NAME,'.'),_USER_COLUMNS.COLUMN_NAME))
		FROM _USER_CONS_COLUMNS,_USER_COLUMNS,_USER_CONSTRAINTS,_USER_INDEXES,_USER_IND_COLUMNS
		WHERE _USER_CONSTRAINTS.CONSTRAINT_TYPE= '4DR'
		AND _USER_CONSTRAINTS.CONSTRAINT_ID = _USER_CONS_COLUMNS.CONSTRAINT_ID
		AND _USER_COLUMNS.TABLE_ID = _USER_CONSTRAINTS.TABLE_ID
		AND _USER_COLUMNS.COLUMN_ID = _USER_CONS_COLUMNS.COLUMN_ID
		AND _USER_COLUMNS.TABLE_ID = _USER_IND_COLUMNS.TABLE_ID
		AND _USER_COLUMNS.COLUMN_ID = _USER_IND_COLUMNS.COLUMN_ID
		AND _USER_INDEXES.INDEX_ID = _USER_IND_COLUMNS.INDEX_ID
		AND _USER_INDEXES.KEYWORD = FALSE
		AND _USER_INDEXES.INDEX_TYPE = :$type
		INTO :$fields;
	End SQL
	
	$results.push(New collection(\
	$indexTypeMap[$type];String(Size of array($fields))).join("\t"))
	
End for each 

SET TEXT TO PASTEBOARD($results.join("\r"))
