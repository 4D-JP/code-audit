METHOD GET PATHS(Path all objects;$methods)

$props:=New collection

$len:=0

For ($m;1;Size of array($methods))
	
	$method:=$methods{$m}
	If (Current method path#$method)
		METHOD GET CODE($method;code;Code with tokens)
		$lines:=Split string(code;"\r";sk trim spaces)
		$lines.shift()
		$len:=$len+$lines.length
		$o:=New object("empty";0;"comment";0;"code";0)
		For each ($line;$lines)
			Case of 
				: (Match regex("\\s*";$line))
					$o.empty:=$o.empty+1
				: (Match regex("^//.*";$line))
					$o.comment:=$o.comment+1
				Else 
					$o.code:=$o.code+1
			End case 
		End for each 
		$props.push($o)
	End if 
	
End for 

$stats:=New object(\
"empty";New object("count";$props.sum("empty");"ratio";$props.sum("empty")/$len);\
"comment";New object("count";$props.sum("comment");"ratio";$props.sum("comment")/$len);\
"code";New object("count";$props.sum("code");"ratio";$props.sum("code")/$len))
