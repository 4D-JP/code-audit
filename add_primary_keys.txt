$logFolder:=Get 4D folder(Logs folder)
$logFileName:="add_primary_key"
$logFileSuffix:=".log"
$logFileIndex:=0

$primaryKeyName:="PK_UUID"

$logPath:=$logFolder+$logFileName+$logFileSuffix
While (Test path name($logPath)=Is a document))
	$logFileIndex:=$logFileIndex+1
	$logPath:=$logFolder+$logFileName+Char(Space)+String($logFileIndex)+$logFileSuffix
End while 

$logFileDocRef:=Create document($logPath)

If (OK=1)
	
	C_TEXT($logMessage)
	USE CHARACTER SET("utf-8";0)
	
	ARRAY LONGINT($TABLE_IDS;0)
	ARRAY TEXT($TABLE_NAMES;0)
	
	Begin SQL
		/* all tables without a primary key */
		SELECT TABLE_ID,TABLE_NAME
		FROM _USER_TABLES
		WHERE _USER_TABLES.TABLE_ID NOT IN 
		(SELECT TABLE_ID FROM _USER_CONSTRAINTS WHERE CONSTRAINT_TYPE = 'P')
		INTO :$TABLE_IDS,:$TABLE_NAMES;
	End SQL
	
	$logMessage:="number of tables with no primary key: "+String(Size of array($TABLE_IDS))+"\n\n"
	SEND PACKET($logFileDocRef;$logMessage)
	
	If (Size of array($TABLE_IDS)#0)
		
		For ($i;1;Size of array($TABLE_IDS))
			$logMessage:=String($TABLE_IDS{$i})+"\t"+$TABLE_NAMES{$i}+"\n"
			SEND PACKET($logFileDocRef;$logMessage)
		End for 
		
		$logMessage:="\nadding primary keys...\n\n"
		SEND PACKET($logFileDocRef;$logMessage)
		
		  //TODO: check for field name conflicts
		
		For ($i;1;Size of array($TABLE_IDS))
			
			$SQL:="ALTER TABLE "+"["+$TABLE_NAMES{$i}+"]"+\
			" ADD TRAILING ["+$primaryKeyName+"] UUID AUTO_GENERATE PRIMARY KEY;"
			
			Begin SQL
				  EXECUTE IMMEDIATE :$SQL;
			End SQL
			
			If (OK=1)
				$logMessage:=String($TABLE_IDS{$i})+"\t"+$TABLE_NAMES{$i}+"\t"+$primaryKeyName+"\n"
				SEND PACKET($logFileDocRef;$logMessage)
			End if 
			
		End for 
		
		$logMessage:="\nadding indexes...\n\n"
		SEND PACKET($logFileDocRef;$logMessage)
		
		For ($i;1;Size of array($TABLE_IDS))
			
			$indexName:=$TABLE_NAMES{$i}+"."+$primaryKeyName
			
			$SQL:="CREATE UNIQUE INDEX "+"["+$indexName+"]"+\
			" ON ["+$TABLE_NAMES{$i}+"] ("+"["+$primaryKeyName+"]);"
			
			Begin SQL
				  EXECUTE IMMEDIATE :$SQL;
			End SQL
			
			If (OK=1)
				$logMessage:=String($TABLE_IDS{$i})+"\t"+$TABLE_NAMES{$i}+"\t"+$indexName+"\n"
				SEND PACKET($logFileDocRef;$logMessage)
			End if 
			
		End for 
		
	End if 
	
	USE CHARACTER SET("utf-8";*)
	CLOSE DOCUMENT($logFileDocRef)
	SHOW ON DISK($logPath)
	
End if 
